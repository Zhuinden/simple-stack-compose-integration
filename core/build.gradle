apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = "com.github.Zhuinden.simple-stack-compose-integration"
version = "0.12.3"

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "com.google.code.findbugs:jsr305:3.0.2"
    api("com.github.Zhuinden:simple-stack:2.9.0") {
        transitive = true
    }
    api "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"

    implementation "androidx.compose.runtime:runtime:1.4.3"
    implementation "androidx.compose.ui:ui:1.4.3"
    implementation "androidx.compose.ui:ui-util:1.4.3"
    implementation "androidx.compose.ui:ui-text:1.4.3"
    implementation "androidx.compose.ui:ui-unit:1.4.3"
    implementation "androidx.compose.ui:ui-geometry:1.4.3"
    implementation "androidx.compose.foundation:foundation:1.4.3"
    implementation "androidx.compose.foundation:foundation-layout:1.4.3"
    implementation "androidx.compose.material:material:1.4.3"
    implementation "androidx.compose.material:material-icons-extended:1.4.3"
    implementation "androidx.compose.animation:animation:1.4.3"
    implementation "androidx.compose.ui:ui-tooling:1.4.3"
    implementation "androidx.activity:activity-compose:1.7.2"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.mockito:mockito-core:3.8.0'
    testImplementation 'org.apache.maven:maven-ant-tasks:2.1.3'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.github.Zhuinden:espresso-helper:1.0.0'
}

task javadoc(type: Javadoc) {
    configurations.implementation.canBeResolved(true)
    configurations.api.canBeResolved(true)

    failOnError false

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    //destinationDir = file("../javadoc/")
    classpath += configurations.api
}

tasks.withType(Javadoc).all {
    enabled = false
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifact(sourcesJar)

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.Zhuinden.simple-stack-compose-integration'
                artifactId = 'core'
                version = '0.12.3'

                pom.withXml {
                    def dependenciesNode = (asNode().get("dependencies") as groovy.util.NodeList).get(0) as groovy.util.Node
                    def configurationNames = ["implementation", "api"]

                    configurationNames.forEach { configurationName ->
                        configurations[configurationName].allDependencies.forEach {
                            if (it.group != null && it.version != "unspecified") {
                                def dependencyNode = dependenciesNode.appendNode("dependency")
                                dependencyNode.appendNode("groupId", it.group)
                                dependencyNode.appendNode("artifactId", it.name)
                                dependencyNode.appendNode("version", it.version)
                                // dependencyNode.appendNode("scope", configurationName)
                            }
                        }
                    }

                    def dependencyNode = dependenciesNode.appendNode("dependency")
                    dependencyNode.appendNode("groupId", "com.github.Zhuinden")
                    dependencyNode.appendNode("artifactId", "state-bundle")
                    dependencyNode.appendNode("version", "1.4.0")
                }
            }
        }
    }
}
